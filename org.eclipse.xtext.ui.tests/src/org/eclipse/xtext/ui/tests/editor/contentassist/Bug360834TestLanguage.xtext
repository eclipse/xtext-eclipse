/*******************************************************************************
 * Copyright (c) 2011 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
grammar org.eclipse.xtext.ui.tests.editor.contentassist.Bug360834TestLanguage
 	with org.eclipse.xtext.common.Terminals

generate bug360834TestLanguage "http://www.eclipse.org/xtext/ui/common/tests/2011/bug360834TestLanguage"

Model :
	  'alternative' element=Alternative
	| 'unordered' element=Unordered
;

Alternative:
    'package' name = FQN ';' 
    rootDeclaration=RecursiveClassDeclaration    
;

FQN:
    ID ('.' ID)*
;

RecursiveClassDeclaration:
    modifiers=AlternativeClassModifiers 'class' name=ID '{'
        members+=RecursiveClassDeclaration* 
    '}' 
;

enum Visibility:
    PUBLIC='public' | PROTECTED='protected' | PRIVATE='private'
;

AlternativeClassModifiers returns Modifiers:
    {Modifiers}
    (
        final?='final'
        |
        abstract?='abstract'   
        |
        extern?='extern'
        | 
        visibility=Visibility?
    )
;

Unordered:
    'package' name = FQN ';' 
    rootDeclaration=SimpleClassDeclaration    
;

SimpleClassDeclaration:
    modifiers=UnorderedModifiers 'class' name=ID '{'
        members+=ClassMember* 
    '}' 
;

ClassMember:
	modifiers=UnorderedModifiers 'var' name=ID ';'
;

UnorderedModifiers:
    {Modifiers}
    (
        final?='final'?
        &
        abstract?='abstract'?   
        &
        extern?='extern'?
        & 
        visibility=Visibility?
    )
;