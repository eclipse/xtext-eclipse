/*******************************************************************************
 * Copyright (c) 2015, 2017 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
grammar org.eclipse.xtext.ui.tests.editor.contentassist.TwoParametersTestLanguage 
	with org.eclipse.xtext.ui.tests.editor.contentassist.ParametersTestLanguage

import "http://www.eclipse.org/2015/tmf/xtext/parameters"

@Override 
ParserRuleParameters: {ParserRuleParameters} 
  ( '#1' scenario=Scenario6<true, true>
  |	'#2' scenario=Scenario6<false, false>
  | '#3' scenario=Scenario7<true, true>
  | '#4' scenario=Scenario7<false, true>
  | =>('#5' scenario=Scenario7<true, true>)
  | =>('#6' scenario=Scenario7<false, false>)
  | '#7' scenario=Scenario8<true, true>
  | '#8' scenario=Scenario8<false, true>
  | '#9' (scenario=Scenario9<true, true> | scenario=Scenario7<true, true> 'trailing'?)
  | '#10' (scenario=Scenario9<true, true> | scenario=Scenario7<false, false> 'trailing'?)
  | '#11' (scenario=Scenario9<false, false> | scenario=Scenario7<true, true> 'trailing'?)
  | '#12' (scenario=Scenario9<false, false> | scenario=Scenario7<false, false> 'trailing'?)
  | '#13' scenario=Scenario10<true, false>
  | '#14' scenario=Scenario10<true, true>
  )
;

Scenario6<A, B> returns Scenario:
	<A & B> first=ID
  | <!A | !B> second=ID
;

Scenario7<A, B> returns Scenario:
	first=IdOrKeyword2<A, B, A&B>
;

Scenario8<A, B> returns Scenario:
	=>first=IdOrKeyword2<A, B, A&B>
	| second='scenario3' // intentionally used keyword from super grammar
;

Scenario9<A, B> returns Scenario:
	=>second=IdOrKeyword2<A, B, A|B> 'scenario4' // intentionally used keyword from super grammar
;

Scenario10<A, B> returns Scenario:
	  <A & B> {Scenario} 'include'
	| {Scenario} 'trailing'
	| {Scenario} 'scenario5' (<A & B> 'include')? first=IdOrKeyword<true> second=IdOrKeyword<false>
	| {Scenario} 'scenario5' 'fragment' Scenario5Body<A & B> 'trailing'
;

IdOrKeyword2<A, B, C>:
	<A & B | C> 'keyword'
	| ID
;
