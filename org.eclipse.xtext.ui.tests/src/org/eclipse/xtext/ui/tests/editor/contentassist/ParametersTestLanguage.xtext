/*******************************************************************************
 * Copyright (c) 2015, 2017 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
grammar org.eclipse.xtext.ui.tests.editor.contentassist.ParametersTestLanguage with org.eclipse.xtext.common.Terminals

generate parametersTestLanguage "http://www.eclipse.org/2015/tmf/xtext/parameters"

ParserRuleParameters: {ParserRuleParameters} 
  ( '#1' scenario=Scenario1<true>
  |	'#2' scenario=Scenario1<Param=false>
  | '#3' scenario=Scenario2<true>
  | '#4' scenario=Scenario2<false>
  | =>('#5' scenario=Scenario2<true>)
  | =>('#6' scenario=Scenario2<false>)
  | '#7' scenario=Scenario3<true>
  | '#8' scenario=Scenario3<false>
  | '#9' (scenario=Scenario4<true> | scenario=Scenario2<true> 'trailing'?)
  | '#10' (scenario=Scenario4<true> | scenario=Scenario2<false> 'trailing'?)
  | '#11' (scenario=Scenario4<false> | scenario=Scenario2<true> 'trailing'?)
  | '#12' (scenario=Scenario4<false> | scenario=Scenario2<false> 'trailing'?)
  | '#13' scenario=Scenario5<false>
  | '#14' scenario=Scenario5<true>
  )
;

Scenario1<Param> returns Scenario:
	<Param> first=ID
  | <!Param> second=ID
;

Scenario2<AllowKeyword> returns Scenario:
	first=IdOrKeyword<AllowKeyword>
;

Scenario3<AllowKeyword> returns Scenario:
	=>first=IdOrKeyword<AllowKeyword>
	| second='scenario3'
;

Scenario4<AllowKeyword> returns Scenario:
	=>second=IdOrKeyword<AllowKeyword> 'scenario4'
;

Scenario5<Include> returns Scenario:
	  <Include> {Scenario} 'include'
	| {Scenario} 'trailing'
	| {Scenario} 'scenario5' (<Include> 'include')? first=IdOrKeyword<true> second=IdOrKeyword<false>
	| {Scenario} 'scenario5' 'fragment' Scenario5Body<Include> 'trailing'
;

fragment Scenario5Body <Include>*:
		<Include> 'include'
	|	<!Include> 'fragment'?
;

IdOrKeyword<Keyword>:
	<Keyword> 'keyword'
	| ID
;
